import "module:std" as std
import "module:spotube_plugin" as spotube
import { EmulatedArtistAlbum } from "../common/album-artist.ht"
import { CommonArtistEntity } from "../common/artist.ht"
import { MbConverter, LbConverter } from "./converter.ht"


var HttpClient = std.HttpClient
var HttpResponse = std.HttpResponse
var RequestOptions = std.RequestOptions
var LocalStorage = spotube.LocalStorage

class ArtistEndpoint {
  var lbApi: HttpClient
  var mbApi: HttpClient
  final username: string

  var emulator: EmulatedArtistAlbum

  var artistCommon: CommonArtistEntity

  construct (this.lbApi, this.mbApi, this.username) {
    this.emulator = EmulatedArtistAlbum(lbApi, mbApi, username)
    this.artistCommon = CommonArtistEntity(lbApi, mbApi, username)
  }

  fun getArtist(id: string) {
    return mbApi.get_req(
      "/artist/${id}",
      queryParameters: {
        fmt: "json",
        inc: "url-rels"
      }.toJson()
    ).then((res){
      var data = res.data

      var wikiId = data["relations"].firstWhere(
        (relation) => relation["type"] == "wikidata",
        orElse: () => null
      )?["url"]?["resource"]?.split("/")?.last

      return artistCommon.getMultipleArtistImages([wikiId]).then((imagesMap){
        var images = imagesMap[wikiId] ?? []
        var artist = MbConverter.artists([data]).first
        artist["images"] = images
        return artist
      })
    })
  }

  fun topTracks(id: string, {limit: int, offset: int}) {
    return mbApi.get_req(
      "/release",
      queryParameters: {
        fmt: "json",
        artist: id,
        limit: 5,
        offset: 0,
        inc: "artist-credits+recordings+ratings+isrcs+release-groups"
      }.toJson()
    ).then((res){
      var data = res.data

      var tracks = data["releases"].expand((release){
        var medias = release["media"].toList()
        release["media"] = null

        return medias.expand((media){
          return media["tracks"].map((track){
            track["recording"]["releases"] = [release]
            return track["recording"]
          }).toList()
        }).toList()
      }).toList()

      // Remove duplicates
      for (var i = 0; i < tracks.length; i++) {
        for (var j = i + 1; j < tracks.length; j++) {
          if (tracks[i]["title"] == tracks[j]["title"]) {
            tracks.removeAt(j)
            j--
          }
        }
      }

      tracks.sort((a, b) {
        var aVoteCount = a["rating"]["votes-count"] ?? 0
        var bVoteCount = b["rating"]["votes-count"] ?? 0
        var aRating = a["rating"]["value"] ?? 0
        var bRating = b["rating"]["value"] ?? 0

        var aAvg = aVoteCount > 0 ? aRating / aVoteCount : 0
        var bAvg = bVoteCount > 0 ? bRating / bVoteCount : 0

        return bAvg.compareTo(aAvg)
      })

      var items = MbConverter.recordings(tracks)

      return {
        limit: limit ?? 10,
        nextOffset: null,
        total: items.length,
        hasMore: false,
        items: items
      }.toJson()
    })
  }

  fun albums(id: string, {offset: int, limit: int}) {
    return mbApi.get_req(
      "/release",
      queryParameters: {
        fmt: "json",
        artist: id,
        limit: limit ?? 20,
        offset: offset ?? 0,
        inc: "artist-credits+release-groups"
      }.toJson()
    ).then((res){
      var data = res.data

      var items = MbConverter.releases(data["releases"])
      var hasMore = data["releases"].length >= (limit ?? 20)

      return {
        limit: limit ?? 20,
        nextOffset: hasMore ? data["release-offset"] + (limit ?? 20) : null,
        total: data["release-count"],
        hasMore: hasMore,
        items: items
      }.toJson()
    })
  }

  fun save(artistIds: List) {
    return emulator.save("artist", artistIds)
  }

  fun unsave(artistIds: List) {
    assert(artistIds.isNotEmpty)
    
    return emulator.unsave("artist", artistIds)
  }
}

export { ArtistEndpoint }